// SPDX-License-Identifier: Apache-2.0 AND GPL-3.0-or-later

plugins {
    id('com.android.application')
    id('dev.rikka.tools.refine') version "${refine_version}"
    id('org.jetbrains.kotlin.android') version "${kotlin_version}"
}

android {
    namespace 'io.github.muntashirakon.AppManager'
    compileSdk compile_sdk
    buildToolsVersion = build_tools

    defaultConfig {
        applicationId 'io.github.muntashirakon.AppManager'
        minSdk min_sdk
        targetSdk target_sdk
        versionCode 445
        versionName "4.0.5"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
        // Add build time to BuildConfig
        buildConfigField "long", "BUILD_TIME_MILLIS", "${buildTime()}"
    }

    signingConfigs {
        debug {
            storeFile file('dev_keystore.jks')
            storePassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyPassword 'kJCp!Bda#PBdN2RLK%yMK@hatq&69E'
            keyAlias 'key0'
        }
        release {
            storeFile file("release.keystore")
            storePassword "password"
            keyPassword "password"
            keyAlias "releasekey"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            signingConfig signingConfigs.debug
        }
    }

    // Distribution flavors to separate OSS and Play Store variants
    flavorDimensions "dist"
    productFlavors {
        oss {
            dimension "dist"
            // Default public name for OSS variant
            resValue "string", "app_name", "App Manager"
        }
        playstore {
            dimension "dist"
            applicationId "com.diganta.appmasterpro"
            // Branding for Play Store builds
            resValue "string", "app_name", "AppMaster Pro"
            // Exclude hidden API dependent sources from Play flavor via source overlay
        }
    }
    lint {
        checkReleaseBuilds false
        abortOnError false
        checkDependencies true
    }
    compileOptions {
        encoding "UTF-8"
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += [
                "-Xjvm-default=all",
        ]
    }
    ndkVersion '25.2.9519653'
    externalNativeBuild {
        cmake {
            version '3.22.1'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    splits {
        abi {
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    aaptOptions {
        noCompress 'jar', 'sh'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
        // Route stubs for Play flavor
        playstore.java.srcDir "src/playstore/java"
        // Exclude hidden-api heavy packages from Play flavor by shadowing with stubs
    }
    dependenciesInfo {
        includeInApk false
        includeInBundle false
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
        resources {
            excludes += ['META-INF/*.version']
            merges += ['baksmali.properties']
        }
    }
    buildFeatures {
        aidl true
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
}

dependencies {
    // Restrict hiddenapi stubs to OSS flavor only
    ossCompileOnly project(path: ':hiddenapi')
    // Add hiddenapi stubs to playstore only at compile time (not packaged)
    playstoreCompileOnly project(path: ':hiddenapi')
    // Refine runtime required at runtime where Refine APIs are referenced
    playstoreImplementation "dev.rikka.tools.refine:runtime:${refine_version}"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:${desugar_jdk_version}"

    // Core Libraries
    implementation project(path: ':libcore:compat')
    implementation project(path: ':libcore:io')
    implementation project(path: ':libcore:ui')
    implementation project(path: ':libserver')
    implementation project(path: ':docs')

    // API
    implementation "com.github.MuntashirAkon:unapkm-android:${unapkm_version}"
    implementation project(path: ':libopenpgp')

    // APK Editing
    implementation "com.github.REAndroid:ARSCLib:${arsclib_version}"
    implementation "com.github.MuntashirAkon:apksig-android:${apksig_version}"
    implementation "com.github.MuntashirAkon:sun-security-android:${sun_security_version}"
    implementation "org.bouncycastle:bcprov-jdk15to18:${bouncycastle_version}"
    implementation "org.bouncycastle:bcpkix-jdk15to18:${bouncycastle_version}"
    implementation "com.android.tools.smali:smali-baksmali:${baksmali_version}"
    implementation "com.android.tools.smali:smali:${baksmali_version}"
    implementation "com.github.MuntashirAkon.jadx:jadx-core:${jadx_version}"
    // Replace SLF4J with a placeholder
    configurations {
        configureEach {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }
    }
    implementation "com.github.MuntashirAkon.jadx:jadx-dex-input:${jadx_version}"
    // Replace SLF4J with a placeholder
    configurations {
        configureEach {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }
    }

    // DB
    implementation "androidx.room:room-runtime:${room_version}"
    annotationProcessor "androidx.room:room-compiler:${room_version}"

    // FM
    implementation "com.j256.simplemagic:simplemagic:${simplemagic_version}"

    // Privileged
    implementation "com.github.MuntashirAkon:libadb-android:${libadb_version}"
    implementation "com.github.topjohnwu.libsu:core:${libsu_version}"
    // Restrict hidden API tooling to OSS flavor only
    ossImplementation "org.lsposed.hiddenapibypass:hiddenapibypass:${hiddenapibypass_version}"
    ossImplementation "dev.rikka.tools.refine:runtime:${refine_version}"

    // UI
    implementation "com.google.android.material:material:${material_version}"
    implementation platform("androidx.compose:compose-bom:${compose_bom}")
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.activity:activity-compose:${activity_version}"

    implementation "androidx.core:core:${androidx_core_version}"
    implementation "androidx.appcompat:appcompat:${appcompat_version}"
    // Fix duplicate classes issue in material
    configurations {
        configureEach {
            exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
        }
    }
    implementation "androidx.documentfile:documentfile:${documentfile_version}"
    implementation "androidx.core:core-splashscreen:${splashscreen_version}"
    implementation "androidx.biometric:biometric:${biometric_version}"
    implementation "androidx.webkit:webkit:${webkit_version}"
    implementation "io.github.Rosemoe.sora-editor:editor:${sora_editor_version}"
    implementation "io.github.Rosemoe.sora-editor:language-textmate:${sora_editor_version}"
    implementation "com.github.MuntashirAkon:time-duration-picker:${duration_picker}"

    // Utility
    implementation "com.google.code.gson:gson:${gson_version}"
    implementation "com.github.luben:zstd-jni:${zstd_version}@aar"

//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    // Espresso UI Testing
//    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    // Optional if you need to detect intents.
//    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"

    // Unit Testing
    testImplementation "junit:junit:${junit_version}"
    testImplementation "org.robolectric:robolectric:${robolectric_version}"
}

preBuild.dependsOn ":server:build"

def buildTime() {
    var commitTime = "git show --no-patch --format=%ct000".execute([], project.rootDir).text.trim()
    if (isDigitsOnly(commitTime)) {
        return Long.parseLong(commitTime)
    }
    println("Using system time as the build time.")
    return System.currentTimeMillis()
}

static def isDigitsOnly(CharSequence str) {
    final int len = str.length()
    for (int cp, i = 0; i < len; i += Character.charCount(cp)) {
        cp = Character.codePointAt(str, i)
        if (!Character.isDigit(cp)) {
            return false
        }
    }
    return true
}
